// Top-level build file where you can add configuration options common to all sub-projects/modules.
// get the latest version from - https://developers.google.com/android/guides/setup
def googlePlayServicesVersion = '15.0.1'

buildscript {
    ext {
        buildToolsVersion = "28.0.3"
        minSdkVersion = 21
        compileSdkVersion = 28
        targetSdkVersion = 28
        supportLibVersion = "28.0.0"
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.android.tools.build:gradle:3.3.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
    }
     configurations.all {
             resolutionStrategy {
                //  force 'com.facebook.android:facebook-android-sdk:4.22.1'

                 // react-native-admob
                 force "com.google.android.gms:play-services-ads:$googlePlayServicesVersion"

                 // force "com.google.android.gms:play-services-base:$googlePlayServicesVersion"
                 // force "com.google.android.gms:play-services-maps:$googlePlayServicesVersion"

                 // react-native-onesignal
                 force "com.google.android.gms:play-services-gcm:$googlePlayServicesVersion"
                 force "com.google.android.gms:play-services-analytics:15.0.2"
                 force "com.google.android.gms:play-services-location:$googlePlayServicesVersion"

                 // react-native-google-sign-in
                 force "com.google.android.gms:play-services-auth:$googlePlayServicesVersion"
             }
         }
}

subprojects { subproject ->
    afterEvaluate{
        if((subproject.plugins.hasPlugin('android') || subproject.plugins.hasPlugin('android-library'))) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion 
                buildToolsVersion rootProject.ext.buildToolsVersion

                lintOptions {
                  abortOnError false
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    distributionUrl = distributionUrl.replace("bin", "all")
}
